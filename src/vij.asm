ISPIES:	LD	IX,SPIESDATA
	LD	IY,SPRITEATR+48
	LD	A,(SPIESDATA)
	OR	A
	JR	Z,ISPIES2
	LD	A,(SPIESDATA+5)
	OR	A
	JP	NZ,PUTSCREENB
	LD	IX,SPIESDATA+5
	LD	IY,SPRITEATR+56
ISPIES2:	LD	A,(SPIESDATA)
	CP	L
	JP	Z,PUTSCREENB
	LD	A,(SPIESDATA+5)
	CP	L
	JP	Z,PUTSCREENB
	LD	(IX+0),L
	LD	(IX+1),L
	LD	(IX+2),H
	LD	(IX+3),0
	LD	(IY+1),E
	LD	(IY+5),E
	LD	BC,(FRANR23)
	LD	A,D
	SUB	C
	SUB	B
	SUB	16
	LD	(IY+0),A
	CP	200
	JP	C,PUTSCREENB
	CP	220
	JP	NC,PUTSCREENB
	ADD	A,16
	LD	(IY+0),A
	LD	(IX+3),16
	JP	PUTSCREENB

SPIEZEN:	LD	HL,SPIESW
	INC	(HL)
	BIT	0,(HL)
	LD	IX,SPIESDATA
	LD	IY,SPRITEATR+48
	JR	Z,SPIEZENB
	LD	IX,SPIESDATA+5
	LD	IY,SPRITEATR+56
SPIEZENB:	INC	(IX+4)
	LD	A,(IX+4)
	CP	28
	JR	C,SPIEZENF
	LD	(IX+4),0
SPIEZENF:	XOR	A
	OR	(IX+0)
	JR	Z,SPIEZENS2
	LD	L,(IX+1)
	LD	H,(IX+2)
	LD	A,(HL)
	AND	11111110B
	CP	140
	JR	NZ,SPIEZENS
	LD	A,(IY+0)
	CP	240
	JR	C,SPIEZENC
	SUB	(IX+3)
	LD	(IY+0),A
	LD	(IX+3),0
SPIEZENC:	CP	220
	JR	NC,SPIEZEND
	CP	208
	JR	NC,SPIEZENS
SPIEZEND:	LD	A,(IY+0)
	SUB	16
	LD	(IY+4),A
	LD	A,(IX+4)
	AND	31
	SRL	A
	CP	8
	JR	C,SPIEZENE
	LD	A,255
SPIEZENE:	INC	A
	SLA	A
	LD	DE,-32
	ADD	HL,DE
	LD	C,(HL)
	LD	E,A
	LD	D,0
	LD	HL,SPIESSPR
	ADD	HL,DE
	LD	A,(HL)
	LD	B,A
	LD	(IY+6),A
	INC	HL
	LD	A,(HL)
	LD	(IY+2),A
	LD	A,C
	CP	153
	RET	Z
	AND	11100000B
	CP	128
	RET	NZ
	LD	A,B
	CP	176
	RET	C
	LD	A,168
	LD	(IY+6),A
	LD	A,172
	LD	(IY+2),A
	RET
SPIEZENS:	LD	(IX+0),0
SPIEZENS2:	LD	(IY+0),219
	LD	(IY+4),219
	RET

SPIESDATA:	DB	0, 0,0, 0, 0	     ;aanwezig,adres,nog van Y af,teller
	DB	0, 0,0, 0, 16

SPIESW:	DB	0	;om en om

SPIESSPR:	DB	160,160	;uit

	DB	160,164	;min
	DB	168,172
	DB	176,180
	DB	184,188	;max
	DB	184,188	;max
	DB	176,180
	DB	168,172
	DB	160,164	;min

IWATER:	LD	B,3
	LD	IX,WATERDATA
	LD	IY,SPRITEATR+28
IWATER2:	PUSH	BC
	XOR	A
	OR	(IX+0)
	JR	Z,IWATER3
	LD	BC,8
	ADD	IX,BC
	LD	C,4
	ADD	IY,BC
	POP	BC
	DJNZ	IWATER2
	JP	PUTSCREENB
IWATER3:	POP	BC
	LD	A,(WATERDATA)
	CP	L
	JP	Z,PUTSCREENB
	LD	A,(WATERDATA+8)
	CP	L
	JP	Z,PUTSCREENB
	LD	A,(WATERDATA+16)
	CP	L
	JP	Z,PUTSCREENB
	LD	(IX+0),L
	LD	(IX+1),L
	LD	(IX+2),H
	LD	(IX+7),10
	LD	A,E
	ADD	A,5
	LD	(IY+1),A
	LD	(IY+0),219
	LD	A,D
	ADD	A,8
	LD	(IX+5),A
	JP	PUTSCREENB

DRUPPEL:	LD	IX,WATERDATA
	LD	IY,SPRITEATR+28
	CALL	DRUPPELB
	LD	IX,WATERDATA+8
	LD	IY,SPRITEATR+32
	CALL	DRUPPELB
	LD	IX,WATERDATA+16
	LD	IY,SPRITEATR+36
DRUPPELB:	LD	A,(IX+5)
	LD	BC,(FRANR23)
	SUB	B
	SUB	C
	LD	C,A
	LD	E,(IX+1)
	LD	D,(IX+2)
	LD	HL,(FRANFIADR2)
	SCF
	SBC	HL,DE
	JP	NC,DRUPPELS2
	LD	HL,(FRANLAADR2)
	SCF
	SBC	HL,DE
	JP	NC,DRUPPELC
DRUPPELS2:	LD	(IX+0),0
DRUPPELC:	EX	DE,HL
	LD	A,(IX+7)
	OR	A
	JR	Z,DRUPPELD
	DEC	(IX+7)
	RET	NZ
	XOR	A
	OR	(IX+0)
	RET	Z
	LD	A,(HL)
	AND	11111100B
	CP	136
	JR	NZ,DRUPPELS
	LD	(IX+6),0
	LD	DE,16
	ADD	HL,DE
	LD	(IX+3),L
	LD	(IX+4),H
	LD	A,C
	ADD	A,4
	LD	(IY+0),A
	RET
DRUPPELD:	LD	A,(IY+0)
	CP	240
	JR	NC,DRUPPELF
	CP	196
	JR	NC,DRUPPELE
DRUPPELF:	ADD	A,2
	LD	(IY+0),A
	LD	A,(IX+6)
	ADD	A,2
	LD	(IX+6),A
	AND	15
	RET	NZ
	LD	L,(IX+3)
	LD	H,(IX+4)
	LD	DE,16
	ADD	HL,DE
	LD	A,(HL)
	AND	11100000B
	CP	128
	JR	Z,DRUPPELE
	LD	(IX+3),L
	LD	(IX+4),H
	RET
DRUPPELE:	LD	(IX+7),32
	LD	(IY+0),219
	RET
DRUPPELS:	LD	(IY+0),219
	LD	(IX+0),0
	RET

WATERDATA:	DB	0, 0,0, 0,0, 0, 0, 0;aanw.,adCON,adVAR,begYcor,teller,teller
	DB	0, 0,0, 0,0, 0, 0, 0
	DB	0, 0,0, 0,0, 0, 0, 0

IKANONR:	LD	B,144
	JR	IKANON
IKANONL:	LD	B,148
IKANON:	LD	A,(WORLDNR)
	CP	6
	JP	Z,PUTSCREENB
	LD	IX,KANONDATA
	LD	IY,SPRITEATR+40
	LD	A,(KANONDATA)
	OR	A
	JR	Z,IKANON2
	LD	A,(KANONDATA+9)
	OR	A
	JP	NZ,PUTSCREENB
	LD	IX,KANONDATA+9
	LD	IY,SPRITEATR+44
IKANON2:	LD	A,(KANONDATA)
	CP	L
	JP	Z,PUTSCREENB
	LD	A,(KANONDATA+9)
	CP	L
	JP	Z,PUTSCREENB
	LD	(IX+0),L
	LD	(IX+1),L
	LD	(IX+2),H
	LD	(IX+5),E
	LD	A,D
	ADD	A,5
	LD	(IX+6),A
	LD	(IX+8),0	;geen kogel bezig
	LD	(IY+0),219
	LD	(IY+2),B
	JP	PUTSCREENB

KANON:	LD	HL,KANONW
	INC	(HL)
	BIT	0,(HL)
	LD	IX,KANONDATA
	LD	IY,SPRITEATR+40
	JR	Z,KANONB
	LD	IX,KANONDATA+9
	LD	IY,SPRITEATR+44
KANONB:	XOR	A
	OR	(IX+0)
	JP	Z,KANONS
	LD	A,(IX+6)
	LD	BC,(FRANR23)
	SUB	B
	SUB	C
	LD	C,A
	LD	E,(IX+1)
	LD	D,(IX+2)
	LD	HL,(FRANFIADR2)
	SCF
	SBC	HL,DE
	JP	NC,KANONS
	LD	HL,(FRANLAADR2)
	SCF
	SBC	HL,DE
	JP	C,KANONS
	EX	DE,HL
KANONC:	BIT	0,(IX+8)
	JR	Z,KANONN
	LD	L,(IX+3)
	LD	H,(IX+4)
	LD	A,(IY+2)
	CP	144
	JR	Z,KANONCR
	LD	A,(IY+1)
	SUB	4
	LD	(IY+1),A
	AND	15
	CP	8
	RET	NZ
	DEC	HL
	JR	KANONCR2
KANONCR:	LD	A,(IY+1)
	ADD	A,4
	LD	(IY+1),A
	AND	15
	RET	NZ
	INC	HL
KANONCR2:	LD	A,(IY+0)
	CP	219
	JR	Z,KANONS2
	LD	A,(HL)
	CP	192
	JP	NC,KANONS2
	AND	11100000B
	CP	128
	JP	Z,KANONS2
	LD	(IX+3),L
	LD	(IX+4),H
	RET
KANONN:	LD	A,(DRAAGDIEN)
	CP	6
	LD	A,(SPRITEATR+4)
	LD	E,-47
	JR	Z,KANONO
	LD	A,(SPRITEATR+12)
	LD	E,-31
KANONO:	SUB	C
	CP	6
	JR	C,KANONP
	CP	E
	JR	NC,KANONP
	CP	-63
	RET	C
	LD	A,(FRANCSTAT)
	CP	2
	RET	NZ
KANONP:	LD	A,(HL)
	AND	11111000B
	CP	144
	JR	NZ,KANONS
	LD	(IX+3),L
	LD	(IX+4),H
	LD	(IY+0),C
	LD	(IX+8),1
	LD	HL,PSGEFF18
	CALL	0001BH
	LD	A,(IY+2)
	CP	144
	JR	Z,KANONPR
	LD	A,(IX+5)
	SUB	4
	LD	(IY+1),A
	RET
KANONPR:	LD	A,(IX+5)
	ADD	A,12
	LD	(IY+1),A
	RET
KANONS:	LD	(IY+0),219
	LD	(IX+0),0
	RET
KANONS2:	LD	(IX+8),0
	LD	(IY+0),219
	RET
KANONW:	DB	0
KANONDATA:	DB	0, 0,0, 0,0, 0,0, 0, 0 ;aanw.,adCON,adVAR,begXcor,begYcor,
	DB	0, 0,0, 0,0, 0,0, 0, 0 ;teller,kogelbezet

	;deze routines hebben betrekking op de vijanden

VIJTOPADR:	DW	0	;pointer van te initialiseren vijanden (top)
VIJBOTADR:	DW	0	;idem (bodem)

VIJADRUPU:	LD	IX,(VIJTOPADR)
	LD	HL,(FRANFIADR)
	CALL	VIJGIVADRE
	LD	(VIJTOPADR),IX
	RET
VIJADRUPD:	LD	IX,(VIJBOTADR)
	LD	HL,(FRANLAADR)
	LD	DE,-16
	ADD	HL,DE
	CALL	VIJGIVADRE
	LD	(VIJBOTADR),IX
	RET

VIJADRDOWU: LD	IX,(VIJTOPADR)
	LD	BC,-15
	ADD	IX,BC
	LD	HL,(FRANFIADR)
	CALL	VIJGIVADRE
	LD	(VIJTOPADR),IX
	RET
VIJADRDOWD: LD	IX,(VIJBOTADR)
	LD	BC,-15
	ADD	IX,BC
	DEC	C
	LD	HL,(FRANLAADR)
	ADD	HL,BC
	CALL	VIJGIVADRE
	LD	(VIJBOTADR),IX
	RET

	;berekent VIJTOPADR en VIJBOTADR
VIJGIVADR:	LD	HL,(FRANFIADR)
	CALL	VIJGIVADRD
	LD	(VIJTOPADR),IX
	LD	HL,(FRANLAADR)
	LD	DE,-16
	ADD	HL,DE
	CALL	VIJGIVADRD
	LD	(VIJBOTADR),IX
	RET
VIJGIVADRD: LD	IX,VIJANDEN
VIJGIVADRE: LD	BC,5
VIJGIVADRB: LD	E,(IX+0)
	LD	D,(IX+1)
	PUSH	HL
	OR	A
	SBC	HL,DE
	POP	HL
	RET	C
	RET	Z
	ADD	IX,BC
	JR	VIJGIVADRB

	;initialiseert alle vijanden op nieuw scherm
INITVIJS:	LD	IX,(VIJTOPADR)
	LD	A,0C9H
	LD	(SPRTOVRM),A
INITVIJSB:	LD	A,(IX+0)
	AND	240
	LD	E,A
	LD	D,(IX+1)
	LD	HL,(FRANLAADR)
	LD	BC,-16
	ADD	HL,BC
	OR	A
	SBC	HL,DE
	JR	C,INITVIJSC
	CALL	INITVIJ
	LD	BC,5
	ADD	IX,BC
	JR	INITVIJSB
INITVIJSC:	XOR	A
	LD	(SPRTOVRM),A
	RET
	;regelt het neerzetten van nieuwe vijanden
INITNEWVT:	DB	0
INITNEWV:	LD	HL,INITNEWVT
	INC	(HL)
	BIT	0,(HL)
	JR	Z,INITNEWVB
	LD	IX,(VIJTOPADR)
	LD	HL,(FRANFIADR)
	JR	INITNEWVC
INITNEWVB:	LD	IX,(VIJBOTADR)
	LD	HL,(FRANLAADR)
	LD	DE,-16
	ADD	HL,DE
INITNEWVC:	PUSH	HL
	LD	A,(IX+0)
	LD	D,(IX+1)
	AND	240
	LD	E,A
	OR	A
	SBC	HL,DE
	JR	NZ,INITNEWVD
	CALL	INITVIJ
	POP	HL
	LD	BC,5
	ADD	IX,BC
	JR	INITNEWVC
INITNEWVD:	POP	HL
	RET

	;initialiseert een vijand. Invoer: IX wijst naar vijandinfo
INITVIJ:	LD	A,(IX+3)
	CP	6
	JP	NC,INITLIFT
	LD	A,(IX+2)
	LD	IY,VIJTABEL
	CP	(IY+0)
	RET	Z
	CP	(IY+10)
	RET	Z
	CP	(IY+20)
	RET	Z
	CP	(IY+30)
	RET	Z
	CP	(IY+40)
	RET	Z
	LD	HL,SPRITEATR+64
	LD	DE,10
	LD	BC,00500H
INITVIJB:	LD	A,(IY+0)
	OR	A
	JR	Z,INITVIJC
	ADD	IY,DE
	LD	E,8
	ADD	HL,DE
	LD	E,10
	INC	C
	DJNZ	INITVIJB
	RET
INITVIJC:	LD	E,(IX+0)
	LD	D,(IX+1)
	LD	A,(DE)
	CP	128
	JR	C,INITVIJC2
	DEC	E
	LD	A,(DE)
	CP	128
	JR	C,INITVIJC2
	INC	E
	INC	E
	LD	A,(DE)
	CP	128
	JR	C,INITVIJC2
	RET
INITVIJC2:	LD	(IY+2),E
	LD	(IY+3),D
	LD	A,(IX+2)
	LD	(IY+0),A
	PUSH	DE
	LD	B,E
	LD	A,(BLOKSUBY)
	NEG
	ADD	A,B
	AND	240
	LD	DE,(FRANR23)
	SUB	E
	SUB	D
	PUSH	HL
	LD	(HL),A
	LD	E,A
	INC	HL
	LD	A,B
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	LD	(HL),A
	INC	HL
	INC	HL
	INC	HL
	LD	(HL),E
	INC	HL
	LD	(HL),A
	LD	(IY+5),0
	LD	(IY+6),0
	LD	A,3
	OUT	(099H),A
	LD	A,142
	OUT	(099H),A
	LD	A,C
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(099H),A
	LD	A,075H
	OUT	(099H),A
	LD	B,0
	LD	C,(IX+3)
	LD	HL,PERVIJAND-1
	ADD	HL,BC
	LD	A,(HL)
	SRL	A
	SRL	A
	SRL	A
	SRL	A
	LD	B,16
INITVIJD:	OUT	(098H),A
	DJNZ	INITVIJD
	LD	A,(HL)
	AND	15
	OR	64
	LD	B,16
INITVIJE:	OUT	(098H),A
	DJNZ	INITVIJE
	POP	DE
	POP	HL
	LD	A,C
	LD	(IY+1),A
	LD	BC,16
	CP	1
	JR	Z,IVOGEL
	CP	2
	JR	Z,IRAT
	CP	3
	JR	Z,IKANGA
	CP	4
	JR	Z,ISPIN
	CP	5
	JR	Z,IKNAGER
	RET
IVOGEL:	LD	A,(IX+4)
	SUB	(IY+2)
	LD	B,1
	JR	NC,IVOGELB
	LD	B,255
	NEG
IVOGELB:	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	LD	(IY+2),A
	LD	(IY+3),A
	LD	(IY+7),B
	LD	(IY+8),0
	LD	(IY+6),5
	RET
IRAT:	ADD	HL,BC
	LD	A,(HL)
	CP	128
	JR	C,ITOCHNIET
	LD	(IY+7),0
	LD	(IY+8),128
	LD	(IY+5),1
	RET
IKANGA:	ADD	HL,BC
	LD	A,(HL)
	CP	128
	JR	C,ITOCHNIET
	LD	(IY+7),0
	LD	(IY+8),136
	LD	(IY+5),20
	RET
ISPIN:	LD	(IY+7),0
	LD	(IY+8),140
	RET
IKNAGER:	ADD	HL,BC
	LD	A,(HL)
	CP	128
	JR	C,ITOCHNIET
	LD	(IY+7),0
	LD	(IY+8),8
	LD	(IY+5),20
	RET
	;de-initialiseren omdat geen grond onder voeten
ITOCHNIET:	LD	(IY+0),0
	EX	DE,HL
	LD	(HL),219
	LD	DE,4
	ADD	HL,DE
	LD	(HL),219
	RET

	;regelt bewegen v. vijanden
VIJBEZT:	DB	0
VIJBEZNR:	DB	0	;nr.van sprite(0-4) voor ontploffing
VIJBEZ:	LD	HL,VIJBEZT
	INC	(HL)
	BIT	0,(HL)
	JR	Z,VIJBEZB
	LD	IX,VIJTABEL
	LD	IY,SPRITEATR+64
	XOR	A
	CALL	VIJBEZC
	LD	IX,VIJTABEL+10
	LD	IY,SPRITEATR+72
	LD	A,1
	JR	VIJBEZC
VIJBEZB:	LD	IX,VIJTABEL+20
	LD	IY,SPRITEATR+80
	LD	A,2
	CALL	VIJBEZC
	LD	IX,VIJTABEL+30
	LD	IY,SPRITEATR+88
	LD	A,3
	CALL	VIJBEZC
	LD	IX,VIJTABEL+40
	LD	IY,SPRITEATR+96
	LD	A,4
VIJBEZC:	LD	(VIJBEZNR),A
	XOR	A
	OR	(IX+0)
	RET	Z
	EX	AF,AF'
	LD	A,(IY+0)
	CP	224
	JR	NC,VIJBEZD
	CP	209
	JR	C,VIJBEZD
	LD	(IY+0),219
	LD	(IY+4),219
	LD	(IX+0),0
	RET
VIJBEZD:	EX	AF,AF'
	CP	255
	JP	Z,ONTPLOF
	LD	A,(IX+5)
	OR	A
	JR	Z,VIJBEZE
	DEC	(IX+5)
	RET
VIJBEZE:	LD	A,(IX+1)
	CP	1
	JP	Z,MVOGEL
	CP	2
	JP	Z,MRAT
	CP	3
	JP	Z,MKANGA
	CP	4
	JP	Z,MSPIN
	CP	5
	JP	Z,MKNAGER
	RET

	;bewegen van knager
MKNAGER:	LD	A,(IX+7)
	LD	B,A
	CP	128
	JP	Z,MKNAGERV	;beweeg knager verticaal
	CP	129
	JP	Z,MKNAGERX	;bezig met knagen
	OR	A
	JP	Z,MKNAGERI
	LD	A,(IY+1)
	LD	C,A
	AND	15
	JR	NZ,MKNAGERB
	LD	L,(IX+2)
	LD	H,(IX+3)
	LD	A,(HL)
	CP	128
	JR	C,MKNAGERB
	LD	A,L
	SUB	B
	LD	(IX+2),A
	LD	(IX+7),0
	RET
MKNAGERB:	LD	A,B
	ADD	A,C
	LD	(IY+1),A
	LD	(IY+5),A
	AND	15
	PUSH	AF
	SRL	A
	SRL	A
	AND	1
	OR	12
	LD	(IX+8),A
	POP	AF
	RET	NZ
MKNAGERI:	LD	(IX+5),25
	LD	(IX+8),8
	LD	L,(IX+2)
	LD	H,(IX+3)
	CALL	MOGELIJK2
	JP	NC,MKNAGERI5
	LD	A,B
	OR	A
	JP	NZ,MKNAGERI2
	CALL	WILLERICH
MKNAGERI2:	CALL	MOGELIJK1
	JR	C,MKNAGERI4
	CALL	MOGELIJK5
	JR	C,MKNAGERI4
	LD	A,B
	NEG
	LD	B,A
	CALL	MOGELIJK1
	JR	C,MKNAGERI4
	CALL	MOGELIJK5
	JR	C,MKNAGERI4
	LD	(IX+7),0
	LD	DE,16
	ADD	HL,DE
	LD	A,(HL)
	CP	128
	RET	C
	CP	154
	RET	NC
	CP	152
	RET	Z
	LD	(IX+6),96
	LD	(IX+7),129
	RET
MKNAGERI4:	LD	A,L
	ADD	A,B
	LD	(IX+2),A
	LD	(IX+7),B
	RET
MKNAGERI5:	LD	(IX+6),8
MKNAGERI6:	LD	(IX+7),128
	LD	(IX+5),0
	LD	DE,16
	ADD	HL,DE
	LD	(IX+2),L
	LD	(IX+3),H
	RET
MKNAGERV:	LD	A,(IY+0)
	ADD	A,2
	LD	(IY+0),A
	LD	(IY+4),A
	DEC	(IX+6)
	RET	NZ
	LD	B,0
	JP	MKNAGERI
MKNAGERX:	DEC	(IX+6)
	JR	Z,MKNAGERX2
	LD	A,(IX+6)
	CP	40
	JR	Z,MKNAGERX3
	CP	95
	JR	Z,MKNAGERX4
	SRL	A
	SRL	A
	AND	3
	OR	8
	LD	(IX+8),A
	RET
MKNAGERX4:	LD	A,(IY+0)
	ADD	A,4
	LD	(IY+0),A
	LD	(IY+4),A
	RET
MKNAGERX3:	LD	L,(IX+2)
	LD	H,(IX+3)
	LD	(VOORRADR),HL
	LD	A,1
	LD	(VOORRFLG),A
	RET
MKNAGERX2:	LD	L,(IX+2)
	LD	H,(IX+3)
	LD	(VOORRADR),HL
	LD	A,2
	LD	(VOORRFLG),A
	LD	(IX+6),6
	LD	(IX+8),8
	JR	MKNAGERI6

	;bewegen van spin
MSPIN:	LD	A,(IX+7)
	LD	B,A
	CP	128
	JP	Z,MSPINV	;beweeg spin verticaal
	OR	A
	JR	Z,MSPINI
	ADD	A,(IY+1)
	LD	(IY+1),A
	LD	(IY+5),A
	AND	15
	PUSH	AF
	SRL	A
	SRL	A
	OR	140
	LD	(IX+8),A
	POP	AF
	RET	NZ
MSPINI:	LD	L,(IX+2)
	LD	H,(IX+3)
	CALL	MOGELIJK2
	JP	NC,MSPINI5
	LD	B,1
	LD	A,(SPRITEATR+21)
	CP	(IY+1)
	JR	NC,MSPINI2
	LD	B,255
MSPINI2:	LD	A,(SPRITEATR+20)
	DEC	A
	CP	(IY+0)
	JR	NC,MSPINI3
	CALL	MOGELIJK1
	JR	C,MSPINI4
	LD	A,B
	NEG
	LD	B,A
	CALL	MOGELIJK1
	JR	C,MSPINI4
	LD	(IX+7),0
	LD	(IX+5),10
	RET
MSPINI3:	CALL	MOGELIJK4
	JR	C,MSPINI4
	LD	A,B
	NEG
	LD	B,A
	CALL	MOGELIJK4
	JR	C,MSPINI4
	LD	(IX+7),0
	LD	(IX+5),10
	RET
MSPINI4:	LD	A,L
	ADD	A,B
	LD	(IX+2),A
	LD	(IX+7),B
	RET
MSPINI5:	LD	(IX+6),8
	LD	(IX+7),128
	LD	DE,16
	ADD	HL,DE
	LD	(IX+2),L
	LD	(IX+3),H
	RET
MSPINV:	LD	A,(IY+0)
	ADD	A,2
	LD	(IY+0),A
	LD	(IY+4),A
	DEC	(IX+6)
	RET	NZ
	JR	MSPINI


	;32 punten horizontaal, 16 omhoog
MKANADDX1:	DB	-3,-3,-2,-2,-1,-2,-1,-1,-1,-1,-1,-1,-1,0,-1,0,-1,0,0
	DB	-1,0,0,0,0,0,1,0,1,1,1,1,2
	;32 punten horizontaal, 16 omlaag
	DB	-2,-1,-1,-1,-1,0,-1,0,0,0,0,0,1,0,0,1
	DB	0,1,0,1,1,1,1,1,1,1,2,1,2,2,3,3
	;32 punten horizontaal
	DB	-2,-1,-1,-1,-1,0,-1,0,0,-1,0,0,-1,0,0,0,0,0,0,0
	DB	1,0,0,1,0,0,1,0,1,1,2,2
	;16 punten horizontaal
	DB	-2,-1,-1,-1,-1,-1,0,-1,0,0,0,0,1,2,2,3

MKANGA:	LD	A,(IX+7)
	LD	B,A
	OR	A
	JR	Z,MKANGAI
	ADD	A,(IY+1)
	LD	(IY+1),A
	LD	(IY+5),A
	LD	A,137
	BIT	7,B
	JR	Z,MKANGA2
	LD	A,139
MKANGA2:	LD	(IX+8),A
	LD	E,(IX+4)
	LD	D,0
	LD	HL,MKANADDX1
	ADD	HL,DE
	LD	A,(IY+0)
	ADD	A,(HL)
	LD	(IY+0),A
	LD	(IY+4),A
	INC	E
	LD	(IX+4),E
	LD	A,E
	CP	32
	JR	Z,MKANGAI
	CP	64
	JR	Z,MKANGAI
	CP	96
	JR	Z,MKANGAI
	CP	112
	RET	NZ
MKANGAI:	LD	(IX+5),20
	LD	L,(IX+2)
	LD	H,(IX+3)
	CALL	MOGELIJK2
	JP	NC,IONTPLOF
	LD	A,B
	OR	A
	JR	NZ,MKANGAI3
	CALL	WILLERICH
MKANGAI3:	CALL	MOGELIJK3	;spring omhoog
	JR	NC,MKANGAI4
	LD	A,B
	NEG
	LD	B,A
	CALL	MOGELIJK3
	JR	NC,MKANGAI4
	LD	(IX+7),0
	JR	MKANGAI7
MKANGAI4:	LD	(IX+7),B
	LD	(IX+4),D
	BIT	0,E
	LD	A,L
	JR	Z,MKANGAI5
	ADD	A,B
MKANGAI5:	ADD	A,B
	LD	L,A
	RES	0,E
	LD	D,0
	BIT	7,E
	JR	Z,MKANGAI6
	LD	D,255
MKANGAI6:	ADD	HL,DE
	LD	(IX+2),L
	LD	(IX+3),H
MKANGAI7:	LD	A,136
	BIT	7,B
	JR	Z,MKANGAI8
	LD	A,138
MKANGAI8:	LD	(IX+8),A
	RET

	;bewegen van rat
MRAT:	LD	A,(IX+7)
	LD	B,A
	OR	A
	JR	Z,MRATI
	ADD	A,(IY+1)
	LD	(IY+1),A
	LD	(IY+5),A
	AND	15
	PUSH	AF
	SRL	A
	SRL	A
	LD	C,A
	LD	A,(IX+8)
	AND	11111100B
	OR	C
	LD	(IX+8),A
	POP	AF
	RET	NZ
MRATI:	LD	L,(IX+2)
	LD	H,(IX+3)
	CALL	MOGELIJK2
	JP	NC,IONTPLOF
	LD	A,B
	OR	A
	JR	NZ,MRATI2
	CALL	WILLERICH
MRATI2:	CALL	MOGELIJK1
	JR	C,MRATI3
	LD	A,B
	NEG
	LD	B,A
	CALL	MOGELIJK1
	JR	C,MRATI3
	LD	(IX+7),0
	RET
MRATI3:	LD	A,128
	BIT	7,B
	JR	Z,MRATI4
	LD	A,132
MRATI4:	LD	(IX+8),A
	LD	A,L
	ADD	A,B
	LD	(IX+2),A
	LD	(IX+7),B
	RET

	;bewegen van vogel
MVOGEL:	INC	(IX+4)
	LD	A,(IX+4)
	SRL	A
	SRL	A
	AND	3
	LD	C,A
	LD	A,(IX+8)
	AND	11111100B
	OR	C
	LD	(IX+8),A
	LD	A,(IX+6)
	OR	A
	JR	Z,MVOGELB
	DEC	(IX+6)
	RET
MVOGELB:	LD	A,(IY+1)
	LD	B,(IX+7)
	ADD	A,B
	LD	(IY+1),A
	LD	(IY+5),A
	DEC	(IX+3)
	RET	NZ
	LD	A,(IX+2)
	LD	(IX+3),A
	LD	A,B
	NEG
	LD	(IX+7),A
	LD	(IX+6),16
	RET

	;initialiseert ontploffing. IX wijst naar vijtabel.(VIJBEZNR)=vijnr
IONTPLOF:	LD	A,(IX+0)
	OR	A
	RET	Z
	CP	255
	RET	Z
	PUSH	HL
	LD	HL,PSGEFF30
	CALL	0001BH
	POP	HL
	LD	(IX+0),255
	LD	(IX+2),0
	LD	(IX+8),14
	LD	A,3
	OUT	(099H),A
	LD	A,142
	OUT	(099H),A
	LD	A,(VIJBEZNR)
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	ADD	A,A
	OUT	(099H),A
	LD	A,075H
	OUT	(099H),A
	LD	A,3
	CALL	IONTPLOFB
	LD	A,73
IONTPLOFB:	LD	B,16
IONTPLOFC:	OUT	(098H),A
	DJNZ	IONTPLOFC
	RET

	;regelt de ontploffing
ONTPLOF:	INC	(IX+2)
	LD	A,(IX+2)
	AND	3
	RET	NZ
	INC	(IX+8)
	LD	A,(IX+8)
	CP	20
	RET	C
	LD	(IX+0),0
	LD	(IY+0),219
	LD	(IY+4),219
	RET

	;laad willekeurig B met 1 of 255
WILLERICH:	LD	A,R
	BIT	2,A
	LD	B,1
	RET	Z
	LD	B,255
	RET

	;zoekt uit hoe kangaroe moet springen. c=1 dan andere richting prob
MOGELIJK3:	PUSH	HL
	LD	C,0
	PUSH	HL
	CALL	MOGELIJK3B
	POP	HL
	LD	DE,-16
	ADD	HL,DE
	PUSH	HL
	CALL	MOGELIJK3B
	POP	HL
	LD	DE,32
	ADD	HL,DE
	CALL	MOGELIJK3B
	LD	DE,16
	ADD	HL,DE
	LD	A,(HL)
	AND	128
	OR	C
	LD	C,A
	POP	HL
	BIT	5,C
	JR	Z,MOGELIJK3C
	SCF
	RET	;andere richting proberen
MOGELIJK3C: LD	DE,000F1H
	LD	A,C
	AND	00011100B
	CP	00010000B
	RET	Z
	LD	DE,06000H
	LD	A,C
	AND	00000010B
	CP	00000010B
	RET	Z
	LD	DE,04001H
	LD	A,C
	AND	00010001B
	CP	1
	RET	Z
	LD	DE,02011H
	LD	A,C
	AND	10010011B
	CP	10000000B
	RET	Z
	SCF
	RET
MOGELIJK3B: LD	A,L
	ADD	A,B
	LD	L,A
	LD	E,(HL)
	RL	E
	RL	C
	ADD	A,B
	LD	L,A
	LD	E,(HL)
	RL	E
	RL	C
	RET

	;als blok onder leeg is, dan c=0
MOGELIJK2:	PUSH	HL
	LD	DE,16
	ADD	HL,DE
	CALL	MOGYESBLOK
	POP	HL
	SCF
	RET

	;invoer: B=richting (1/255)
	;Als blok naast is leeg en daaronder is vol, dan c=1
MOGELIJK1:	PUSH	HL
	LD	A,L
	ADD	A,B
	LD	L,A
	CALL	MOGNOBLOK
	LD	DE,16
	ADD	HL,DE
	CALL	MOGYESBLOK
	POP	HL
	SCF
	RET
	;invoer: B=richting (1/255)
	;Als blok naast is leeg en daaronder leeg en daaronder vol dan c=1
MOGELIJK5:	PUSH	HL
	LD	A,L
	ADD	A,B
	LD	L,A
	CALL	MOGNOBLOK
	LD	DE,16
	ADD	HL,DE
	CALL	MOGNOBLOK
	ADD	HL,DE
	CALL	MOGYESBLOK
	POP	HL
	SCF
	RET
	;invoer: B=richting (1/255)
	;Als blok naast is leeg.
MOGELIJK4:	PUSH	HL
	LD	A,L
	ADD	A,B
	LD	L,A
	CALL	MOGNOBLOK
	POP	HL
	SCF
	RET
MOGNOBLOK:	LD	A,(HL)
	CP	128
	RET	C
	POP	AF
	POP	HL
	OR	A
	RET
MOGYESBLOK: LD	A,(HL)
	CP	128
	RET	NC
	POP	AF
	POP	HL
	OR	A
	RET

	;sprite-kleuren per vijand
PERVIJAND:	DB	07BH	;vogel    -1
	DB	069H	;rat      -2
	DB	03DH	;kangaroe -3
	DB	0A5H	;spin     -4
	DB	06BH	;knager   -5

VIJTABEL:	DB	0	;nr. van vijand (0-255).0=LEEG
	DB	0	;soort vijand
	DW	0	;adres vijand
	DB	0	;extra info. Per vijand verschillend.
	DB	0	;wachtbyte (knager en voor jump kangaroe)
	DB	0	;teller bij bewegen op pixelniveau
	DB	0	;richting vijand
	DB	0	;standnr. van sprite (aanvraag)
	DB	0	;standnr. van sprite (werkelijk)
	DS	40	;de 4 andere vijanden

	;routines die betrekking hebben op de lift

	;IX=pointer naar vijandtabel(+0/1=adres,+2=unieke code,+3=6)
	;+4= extra info: b7=0 als optellen, b6=0 als horizont,b0-5=aantal
INITLIFT:	LD	IY,LIFTDATA
	LD	A,(IX+2)
	CP	(IY+0)
	RET	Z
	CP	(IY+9)
	RET	Z
	BIT	0,(IX+3)
	JR	Z,INITLIFTB
	LD	IY,LIFTDATA+9
INITLIFTB:	LD	A,(IX+2)
	LD	(IY+0),A
	LD	(IY+1),0
	LD	L,(IX+0)
	LD	H,(IX+1)
	LD	(IY+2),L
	LD	(IY+6),L
	LD	(IY+3),H
	LD	(IY+7),H
	LD	A,(IX+4)
	LD	(IY+8),A
	RES	0,(IY+8)
	AND	63
	LD	C,A
	LD	B,0
	BIT	6,(IX+4)
	JR	Z,INITLIFTC
	SLA	C
	SLA	C
	SLA	C
	RL	B
	SLA	C
	RL	B
INITLIFTC:	BIT	7,(IX+4)
	JR	Z,INITLIFTD
	OR	A
	SBC	HL,BC
	JR	INITLIFTE
INITLIFTD:	ADD	HL,BC
INITLIFTE:	LD	(IY+4),L
	LD	(IY+5),H
	RET

	;invoer: IX=pointer naar liftdata, IY=pointer naar spr.atr.tabel
MLIFTT:	DB	0
MLIFT:	LD	HL,MLIFTT
	INC	(HL)
	LD	IX,LIFTDATA
	LD	IY,SPRITEATR+112
	BIT	0,(HL)
	JR	Z,MLIFTX
	LD	IX,LIFTDATA+9
	LD	IY,SPRITEATR+120
MLIFTX:	XOR	A
	OR	(IX+0)
	JR	NZ,MLIFTY
	LD	(IY+0),219
	LD	(IY+4),219
	RET
MLIFTY:	LD	A,(IX+2)
	LD	C,A
	AND	240
	LD	L,A
	LD	H,(IX+3)
	LD	A,(IX+4)
	LD	B,A
	AND	240
	LD	E,A
	LD	D,(IX+5)
	OR	A
	LD	E,B	;DE=naar
	LD	B,H	;BC=van
	LD	L,(IX+6)	;HL=huidig
	LD	H,(IX+7)
	BIT	6,(IX+8)
	JP	Z,MLIFTHORI
	;lift verticaal bewegen
	LD	A,D
	CP	B
	JR	C,MLIFTVB
	JR	NZ,MLIFTVC
	LD	A,E
	CP	C
	JR	C,MLIFTVB
MLIFTVC:	INC	(IX+1)
	LD	A,(IX+1)
	CP	16
	JR	Z,MLIFTVD
	JR	MLIFTVG
MLIFTVB:	DEC	(IX+1)
	LD	A,(IX+1)
	CP	240
	JR	NZ,MLIFTVG
MLIFTVD:	LD	(IX+1),0
	PUSH	DE
	LD	E,A
	LD	D,0
	BIT	7,E
	JR	Z,MLIFTVE
	LD	D,255
MLIFTVE:	ADD	HL,DE
	POP	DE
	PUSH	HL
	OR	A
	SBC	HL,DE
	POP	HL
	JR	Z,MLIFTVF
	PUSH	HL
	OR	A
	SBC	HL,BC
	POP	HL
	JR	NZ,MLIFTVG
MLIFTVF:	LD	(IX+2),E
	LD	(IX+3),D
	LD	(IX+4),C
	LD	(IX+5),B
MLIFTVG:	LD	(IX+6),L
	LD	(IX+7),H
	LD	A,L
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	LD	(IY+1),A
	LD	(IY+5),A
	LD	DE,(FRANFIADR)
	LD	A,H
	CP	D
	JR	C,MLIFTVJ
	JR	NZ,MLIFTVI
	LD	A,L
	CP	E
	JR	C,MLIFTVJ
MLIFTVI:	LD	DE,(FRANLAADR)
	LD	A,H
	CP	D
	JR	C,MLIFTVH
	JR	NZ,MLIFTVJ
	LD	A,L
	CP	E
	JR	C,MLIFTVH
MLIFTVJ:	LD	(IY+0),219
	LD	(IY+4),219
	JR	MLIFTCV	;kijken of franc op lift
MLIFTVH:	LD	A,(BLOKSUBY)
	LD	B,A
	LD	A,L
	AND	240
	SUB	B
	ADD	A,(IX+1)
	LD	BC,(FRANR23)
	SUB	B
	SUB	C
	LD	(IY+0),A
	LD	(IY+4),A
MLIFTCV:	RES	0,(IX+8)
	LD	A,(SPRITEATR+21)
	SUB	(IY+1)
	CP	11
	JR	C,MLIFTCVB
	CP	245
	CCF
	RET	NC
MLIFTCVB:	LD	A,(SPRITEATR+20)
	SUB	(IY+0)
	CP	0F5H
	RET	NC
	CP	0ECH
	CCF
	RET	NC
	ADD	A,16
	NEG
	LD	C,A
	LD	A,(FRANCSTAT)
	CP	3
	RET	NC
	CP	1
	RET	Z
	SET	0,(IX+8)
	CALL	VERTFRANC
	XOR	A
	LD	(FRANCSTAT),A
	SCF
	RET
	;lift horizontaal bewegen
MLIFTHORI:	LD	A,E
	CP	C
	JR	C,MLIFTHORIB
	LD	A,1
	LD	(MLIFTCHO+1),A
	INC	(IX+1)
	LD	A,(IX+1)
	CP	16
	JR	Z,MLIFTHORIC
	JR	MLIFTHORIE
MLIFTHORIB: LD	A,-1
	LD	(MLIFTCHO+1),A
	DEC	(IX+1)
	LD	A,(IX+1)
	CP	240
	JR	NZ,MLIFTHORIE
MLIFTHORIC: LD	(IX+1),0
	SRA	A
	SRA	A
	SRA	A
	SRA	A
	ADD	A,L
	LD	L,A
	LD	(IX+6),A
	CP	E
	JR	Z,MLIFTHORID
	CP	C
	JR	NZ,MLIFTHORIE
MLIFTHORID: LD	(IX+2),E
	LD	(IX+3),D
	LD	(IX+4),C
	LD	(IX+5),B
MLIFTHORIE: LD	(IX+6),L
	LD	A,L
	SLA	A
	SLA	A
	SLA	A
	SLA	A
	ADD	A,(IX+1)
	LD	(IY+1),A
	LD	(IY+5),A
	LD	DE,(FRANFIADR)
	LD	A,H
	CP	D
	JR	C,MLIFTHORIF
	JR	NZ,MLIFTHORIG
	LD	A,L
	CP	E
	JR	C,MLIFTHORIF
MLIFTHORIG: LD	DE,(FRANLAADR)
	LD	A,H
	CP	D
	JR	C,MLIFTHORIH
	JR	NZ,MLIFTHORIF
	LD	A,L
	CP	E
	JR	C,MLIFTHORIH
MLIFTHORIF: LD	(IY+0),219
	LD	(IY+4),219
	JR	MLIFTCH
MLIFTHORIH: LD	A,(BLOKSUBY)
	LD	B,A
	LD	A,L
	AND	240
	SUB	B
	LD	BC,(FRANR23)
	SUB	B
	SUB	C
	LD	(IY+0),A
	LD	(IY+4),A
MLIFTCH:	CALL	MLIFTCV
	RET	NC
MLIFTCHO:	LD	C,0
	JP	HORIFRANC

	;unieke code,teller,van adres, naar adres, huidig adres,hor/vert
LIFTDATA:	DB	0, 0, 0,0, 0,0, 0,0, 0     ;lift1
	DB	0, 0, 0,0, 0,0, 0,0, 0     ;lift2

SPRAAK:	LD	HL,(SPRITEATR)
	LD	BC,(SPRITEATR+104)
	INC	L
	INC	H
	LD	A,5
	LD	IX,VIJTABEL
	LD	IY,SPRITEATR+64
SPRAAKC:	PUSH	AF
	CALL	SPRAAKB
	LD	DE,10
	ADD	IX,DE
	LD	E,8
	ADD	IY,DE
	POP	AF
	DEC	A
	JR	NZ,SPRAAKC
	RET
SPRAAKB:	XOR	A
	OR	(IX+0)
	RET	Z
	LD	E,(IY+0)
	LD	D,(IY+1)
	;Invoer:HL=x,y spuug
	;	 DE=x,y vijand
	LD	A,L
	CP	192
	JP	NC,SPRAAKD
	SUB	E
	CP	16
	JP	NC,SPRAAKD
	LD	A,H
	SUB	D
	CP	16
	JP	NC,SPRAAKD
	POP	AF
	POP	AF
	;spuug raakt vijand
	XOR	7
	SUB	2
	LD	(VIJBEZNR),A
	CALL	SPUGEND
	LD	A,(IX+1)
	CP	1
	JP	Z,IONTPLOF
	CP	4
	JP	NC,IONTPLOF
	CP	3
	JR	Z,BEVRIES
	LD	A,(IX+7)
	CP	1
	JR	Z,RATTERUG
	CP	255
	RET	NZ
RATTERUG:	NEG
	LD	(IX+5),15
	LD	B,A
	LD	A,(IY+1)
	AND	15
	RET	Z
	LD	(IX+7),B
	LD	A,(IX+2)
	ADD	A,B
	LD	(IX+2),A
	LD	A,(IX+8)
	XOR	4
	LD	(IX+8),A
	RET
BEVRIES:	LD	(IX+5),160
	RET

SPRAAKD:	LD	A,C
	CP	192
	RET	NC
	;Invoer:BC=x,y blok
	;	 DE=x,y vijand
	SUB	E
	CP	16
	JR	C,SPRAAKE
	CP	241
	RET	C
SPRAAKE:	LD	A,B
	SUB	D
	CP	16
	JR	C,SPRAAKF
	CP	241
	RET	C
	;blok raakt vijand
SPRAAKF:	POP   AF
	POP   AF
	XOR   7
	SUB   2
	LD    (VIJBEZNR),A
	JP    IONTPLOF

FRAAKT:	DB	0	;om en om, dienblad testen, franc testen
FRAAK:	LD	HL,FRAAKT
	INC	(HL)
	BIT	0,(HL)
	JP	Z,DRAAK
FRAAKB:	LD	A,(FRANCKNIP)
	OR	A
	RET	NZ
	LD	A,(FRANCSTAT)
	CP	3
	JP	Z,FRAAKC
	CP	4
	JP	Z,FRAAKC
	LD	HL,(SPRITEATR+12)
	LD	DE,VIJTABEL
	LD	BC,(SPRITEATR+64)
	CALL	PLAK2
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+10
	LD	BC,(SPRITEATR+72)
	CALL	PLAK2
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+20
	LD	BC,(SPRITEATR+80)
	CALL	PLAK2
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+30
	LD	BC,(SPRITEATR+88)
	CALL	PLAK2
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+40
	LD	BC,(SPRITEATR+96)
	CALL	PLAK2
	JP	C,RAAKVIJ

	LD	BC,(SPRITEATR+28)
	LD	DE,SPRITEATR+28
	CALL	PLAK4
	JP	C,RAAKDRUP
	LD	BC,(SPRITEATR+32)
	LD	DE,SPRITEATR+32
	CALL	PLAK4
	JP	C,RAAKDRUP
	LD	BC,(SPRITEATR+36)
	LD	DE,SPRITEATR+36
	CALL	PLAK4
	JP	C,RAAKDRUP

	LD	BC,(SPRITEATR+40)
	LD	DE,SPRITEATR+40
	CALL	PLAK6
	JP	C,RAAKKAN
	LD	BC,(SPRITEATR+44)
	LD	DE,SPRITEATR+44
	CALL	PLAK6
	JP	C,RAAKKAN

	LD	BC,(SPRITEATR+48)
	LD	DE,(SPRITEATR+50)
	CALL	PLAK8
	JP	C,RAAKSPIE
	LD	BC,(SPRITEATR+56)
	LD	DE,(SPRITEATR+58)
	CALL	PLAK8
	JP	C,RAAKSPIE
	RET

FRAAKCT:	DB	0	;bij bukken om en om testen
FRAAKC:	LD	HL,FRAAKCT
	INC	(HL)
	BIT	0,(HL)
	LD	HL,(SPRITEATR+12)
	JR	Z,FRAAKD
	LD	HL,(SPRITEATR+20)
FRAAKD:	LD	DE,VIJTABEL
	LD	BC,(SPRITEATR+64)
	CALL	PLAK1
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+10
	LD	BC,(SPRITEATR+72)
	CALL	PLAK1
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+20
	LD	BC,(SPRITEATR+80)
	CALL	PLAK1
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+30
	LD	BC,(SPRITEATR+88)
	CALL	PLAK1
	JP	C,RAAKVIJ
	LD	DE,VIJTABEL+40
	LD	BC,(SPRITEATR+96)
	CALL	PLAK1
	JP	C,RAAKVIJ

	LD	BC,(SPRITEATR+28)
	LD	DE,SPRITEATR+28
	CALL	PLAK3
	JP	C,RAAKDRUP
	LD	BC,(SPRITEATR+32)
	LD	DE,SPRITEATR+32
	CALL	PLAK3
	JP	C,RAAKDRUP
	LD	BC,(SPRITEATR+36)
	LD	DE,SPRITEATR+36
	CALL	PLAK3
	JP	C,RAAKDRUP

	LD	BC,(SPRITEATR+40)
	LD	DE,SPRITEATR+40
	CALL	PLAK5
	JP	C,RAAKKAN
	LD	BC,(SPRITEATR+44)
	LD	DE,SPRITEATR+44
	CALL	PLAK5
	JP	C,RAAKKAN

	LD	BC,(SPRITEATR+48)
	LD	DE,(SPRITEATR+50)
	CALL	PLAK7
	JP	C,RAAKSPIE
	LD	BC,(SPRITEATR+56)
	LD	DE,(SPRITEATR+58)
	CALL	PLAK7
	JP	C,RAAKSPIE
	RET

	;1 sprite van FRANC met vijand
	;HL=x,y FRANC, BC=x,y vijand
PLAK1:	LD	A,H
	SUB	B
	CP	13
	JR	C,PLAK1B
	CP	244
	CCF
	RET	NC
PLAK1B:	LD	A,L
	SUB	C
	CP	13
	RET	C
	CP	244
	CCF
	RET

	;2 onder elkaar liggende sprites van FRANC met vijand
	;HL=x,y FRANC, BC=x,y vijand
PLAK2:	LD	A,H
	SUB	B
	CP	13
	JR	C,PLAK2B
	CP	244
	CCF
	RET	NC
PLAK2B:	LD	A,L
	SUB	C
	CP	13
	RET	C
	CP	228
	CCF
	RET

	;1 sprite van FRANC met druppel
	;HL=x,y FRANC, BC=x,y druppel
PLAK3:	LD	A,H
	SUB	B
	CP	4
	JR	C,PLAK3B
	CP	242
	CCF
	RET	NC
PLAK3B:	LD	A,L
	SUB	C
	CP	2
	RET	C
	CP	244
	CCF
	RET

	;2 onder elkaar liggende sprites van FRANC met druppel
	;HL=x,y FRANC, BC=x,y druppel
PLAK4:	LD	A,H
	SUB	B
	CP	4
	JR	C,PLAK4B
	CP	242
	CCF
	RET	NC
PLAK4B:	LD	A,L
	SUB	C
	CP	4
	RET	C
	CP	228
	CCF
	RET

	;1 sprite van FRANC met kanonskogel
	;HL=x,y FRANC, BC=x,y kanonskogel
PLAK5:	LD	A,H
	SUB	B
	CP	4
	JR	C,PLAK5B
	CP	244
	CCF
	RET	NC
PLAK5B:	LD	A,L
	SUB	C
	CP	3
	RET	C
	CP	244
	CCF
	RET

	;2 onder elkaar liggende sprites van FRANC met kanonskogel
	;HL=x,y FRANC, BC=x,y kanonskogel
PLAK6:	LD	A,H
	SUB	B
	CP	4
	JR	C,PLAK6B
	CP	244
	CCF
	RET	NC
PLAK6B:	LD	A,L
	SUB	C
	CP	2
	RET	C
	CP	228
	CCF
	RET

	;1 sprite van FRANC met spiezen
	;HL=x,y FRANC, BC=x,y onderste spies-sprite
PLAK7:	LD	IY,PLAK8E
	JR	PLAK8C

	;2 onder elkaar liggende sprites van FRANC met spiezen
	;HL=x,y FRANC, BC=x,y onderste spies-sprite
PLAK8:	LD	IY,PLAK8D
PLAK8C:	LD	A,H
	SUB	B
	CP	13
	JR	C,PLAK8B
	CP	244
	CCF
	RET	NC
PLAK8B:	LD	A,E
	CP	160
	RET	Z
	SUB	164
	SRL	A
	SRL	A
	SRL	A
	AND	3
	LD	D,0
	LD	E,A
	ADD	IY,DE
	LD	A,L
	SUB	C
	CP	14
	RET	C
	CP	(IY+0)
	CCF
	RET
PLAK8D:	DB	235,227,219,211
PLAK8E:	DB	251,243,235,227

	;dienblad van FRANC met kanonskogel
	;HL=x,y dienbl., BC=x,y kanonskogel
PLAK9:	LD	A,H
	SUB	B
	CP	2
	JR	C,PLAK9B
	CP	246
	CCF
	RET	NC
PLAK9B:	LD	A,L
	SUB	C
	CP	3
	RET	C
	CP	244
	CCF
	RET

	;dienblad van FRANC met vijand
	;HL=x,y dienblad, BC=x,y vijand
PLAKA:	LD	A,H
	SUB	B
	CP	11
	JR	C,PLAKAB
	CP	246
	CCF
	RET	NC
PLAKAB:	LD	A,L
	SUB	C
	CP	13
	RET	C
	CP	244
	CCF
	RET

	;rotsblok met kanonskogel
	;HL=x,y dienbl., BC=x,y kanonskogel
PLAKB:	LD	A,H
	SUB	B
	CP	8
	JR	C,PLAKBB
	CP	241
	CCF
	RET	NC
PLAKBB:	LD	A,L
	SUB	C
	CP	3
	RET	C
	CP	241
	CCF
	RET

	;checkt of dienblad druppels,spiezen,kogels raakt
DRAAK:	LD	HL,(SPRITEATR+104)
	LD	A,L
	CP	192
	JR	NC,DRAAK2
	;raakt kanonskogel rotsen?
	LD	BC,(SPRITEATR+40)
	LD	DE,SPRITEATR+40
	CALL	PLAKB
	JP	C,RAAKKAN3
	LD	BC,(SPRITEATR+44)
	LD	DE,SPRITEATR+44
	CALL	PLAKB
	JP	C,RAAKKAN3

DRAAK2:	LD	A,(DIENSTUK)
	OR	A
	RET	NZ
	LD	A,(FRANCSTAT)
	CP	4
	JR	Z,DRAAK2B
	LD	A,(DRAAGDIEN)
	CP	6
	JR	NZ,DRAAKVER
DRAAK2B:	LD	A,(FRANCKNIP)
	OR	A
	RET	NZ
DRAAKVER:	LD	HL,(SPRITEATR+4)
	LD	A,L
	CP	184
	RET	NC
	LD	BC,(SPRITEATR+28)
	LD	DE,SPRITEATR+28
	CALL	PLAK3
	JP	C,RAAKDRUP2
	LD	BC,(SPRITEATR+32)
	LD	DE,SPRITEATR+32
	CALL	PLAK3
	JP	C,RAAKDRUP2
	LD	BC,(SPRITEATR+36)
	LD	DE,SPRITEATR+36
	CALL	PLAK3
	JP	C,RAAKDRUP2

	LD	BC,(SPRITEATR+40)
	LD	DE,SPRITEATR+40
	CALL	PLAK9
	JP	C,RAAKKAN2
	LD	BC,(SPRITEATR+44)
	LD	DE,SPRITEATR+44
	CALL	PLAK9
	JP	C,RAAKKAN2

	LD	BC,(SPRITEATR+48)
	LD	DE,(SPRITEATR+50)
	CALL	PLAK7
	JP	C,RAAKSPIE2
	LD	BC,(SPRITEATR+56)
	LD	DE,(SPRITEATR+58)
	CALL	PLAK7
	JP	C,RAAKSPIE2

	LD	A,(FRANCSTAT)
	CP	4
	JR	Z,DRAAK3
	LD	A,(DRAAGDIEN)
	CP	6
	RET	NZ
DRAAK3:	LD	DE,VIJTABEL
	LD	BC,(SPRITEATR+64)
	CALL	PLAKA
	JP	C,RAAKDIEN
	LD	DE,VIJTABEL+10
	LD	BC,(SPRITEATR+72)
	CALL	PLAKA
	JP	C,RAAKDIEN
	LD	DE,VIJTABEL+20
	LD	BC,(SPRITEATR+80)
	CALL	PLAKA
	JP	C,RAAKDIEN
	LD	DE,VIJTABEL+30
	LD	BC,(SPRITEATR+88)
	CALL	PLAKA
	JP	C,RAAKDIEN
	LD	DE,VIJTABEL+40
	LD	BC,(SPRITEATR+96)
	CALL	PLAKA
	RET	NC

RAAKDIEN:	LD	A,(DE)
	OR	A
	RET	Z
	CP	255
	RET	Z
	LD	A,(FRANCSTAT)
	CP	4
	JR	Z,RAAKVIJB
	JP	INITDIENV

RAAKVIJ:	LD	A,(DE)
	OR	A
	RET	Z
	CP	255
	RET	Z
RAAKVIJB:	INC	DE
	LD	A,(DE)
	LD	B,5
	CP	1
	JR	Z,RAAKVIJ2
	CP	2
	JR	Z,RAAKVIJ3
	LD	B,8
	CP	3
	JR	Z,RAAKVIJ2
	LD	B,6
	CP	4
	JR	Z,RAAKVIJ2
	LD	B,5
	CP	5
	RET	NZ
RAAKVIJ2:	CALL	POWERDEC
	LD	A,100
	LD	(FRANCKNIP),A
	LD	HL,PSGEFF25
	JP	0001BH
RAAKVIJ3:	LD	A,1
	LD	(VERGIF),A
	LD	A,100
	LD	(FRANCKNIP),A
	LD	HL,PSGEFF27
	JP	0001BH

RAAKDRUP:	LD	A,219
	LD	(DE),A
	LD	B,2
	JR	RAAKVIJ2

RAAKSPIE:	LD	B,4
	JR	RAAKVIJ2

RAAKKAN:	LD	A,219
	LD	(DE),A
	LD	B,3
	JR	RAAKVIJ2

RAAKKAN3:	LD	A,219
	LD	(DE),A
	RET

RAAKDRUP2:	JR	RAAKDIENBL
RAAKKAN2:	JR	RAAKDIENBL
RAAKSPIE2:	JR	RAAKDIENB2

RAAKDIENBL: LD	A,219
	LD	(DE),A
RAAKDIENB2: LD	A,(FRANCSTAT)
	CP	5
	RET	Z
	CP	4
	LD	B,3
	JP	Z,RAAKVIJ2
	LD	A,(HEBDEKSEL)
	OR	A
	LD	HL,PSGEFF39
	JP	NZ,0001BH
	LD	A,(DRAAGDIEN)
	CP	6
	JP	Z,INITDIENV
	LD	A,(SPRITEATR+4)
	CP	184
	RET	NC
	JP	SETSTUK