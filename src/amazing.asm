	;neemt geen rode scrolletje mee en geen muziek
AMAZING:
	DI
	CALL	INSTMUSINT
	LD	(AMAZSTACK),SP
	LD	BC,00201H
	CALL	WRTVDP
	CALL	SPRITEPAG1

	EI
	LD	HL,CLRSCRCOMM
	CALL	IVDPCOMM
	CALL	CHECKVDP
	DI
	LD	BC,04201H	;scherm aan
	CALL	WRTVDP
AMAZCONT:
	DI
	LD	BC,00017H
	CALL	WRTVDP
	LD	BC,0D413H
	CALL	WRTVDP
	CALL	CLEARSPR
	CALL	SPRTOVRAM
	LD	HL,SPRITEATR2
	LD	DE,SPRITEATR
	LD	BC,100
	LDIR
	LD	HL,0F400H
	LD	DE,SPRCOLAMAZ
	OR	A
	CALL	SETSPRCOL	;zet spritekleurentabel
	LD	HL,0F580H
	OR	A
	CALL	SETWRVRAM
	LD	B,128
	XOR	A
AMAZINGBB:
	OUT	(098H),A
	NOP
	DJNZ	AMAZINGBB
	LD	HL,PALLETROOD
	LD	DE,PALLET4
	LD	A,3
	CALL	LETFADE
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	LD	(AMAZINT2+4),HL
	LD	HL,AMAZINT2
	LD	(INTERCALL),HL
	CALL	READINT
	LD	B,6
	CALL	PAUSE
	;letters amazing op scherm
	LD	IX,AMAZCOMDAT
	LD	B,7
AMAZINGC:
	PUSH	BC
	LD	A,(IX+0)
	LD	(AMAZCOMM2),A
	LD	A,(IX+1)
	LD	(AMAZCOMM2+2),A
	LD	A,(IX+2)
	LD	(AMAZCOMM2+8),A
	LD	A,(IX+3)
	LD	(AMAZCOMM2+4),A
	LD	HL,AMAZCOMM2
	CALL	IVDPCOMM
	LD	HL,FMEFF5
	LD	DE,PSGEFF31
	CALL	EFFECTOR
	LD	B,10
	CALL	PAUSE
	LD	DE,4
	ADD	IX,DE
	POP	BC
	DJNZ	AMAZINGC
	;plasma naar rechts
	LD	B,85
AMAZINGD:
	EI
	HALT
	LD	A,(SPRITEATR+65)
	ADD	A,2
	LD	(SPRITEATR+65),A
	LD	(SPRITEATR+69),A
	DJNZ	AMAZINGD
	LD	B,15
	CALL	PAUSE
	LD	HL,PSGEFF22
	CALL	SETPSGEFF
	LD	B,4
AMAZINGE:
	PUSH	BC
	LD	A,(SPRITEATR+66)
	ADD	A,4
	LD	(SPRITEATR+66),A
	ADD	A,20
	LD	(SPRITEATR+70),A
	LD	B,5
	CALL	PAUSE
	POP	BC
	DJNZ	AMAZINGE
	LD	B,20
	CALL	PAUSE
	LD	HL,FMEFF2
	LD	DE,PSGEFF27
	CALL	EFFECTOR
	LD	A,172
	LD	(SPRITEATR+88),A
	LD	B,17
AMAZINGF:
	EI
	HALT
	LD	A,(SPRITEATR+88)
	SUB	3
	LD	(SPRITEATR+88),A
	DJNZ	AMAZINGF
	LD	HL,FMEFF10
	LD	DE,PSGEFF15
	CALL	EFFECTOR
	LD	A,196
	LD	(SPRITEATR+90),A
	DI
	LD	HL,AMAZKOGEL
	LD	(AMAZINT2+1),HL
	LD	BC,09613H
	CALL	WRTVDP
	CALL	READINT
	EI
	LD	A,90
	LD	(AMAZCOMM6+2),A
	SUB	4
	LD	(AMAZCOMM6+6),A
	LD	B,32
AMAZINGG:
	PUSH	BC
	EI
	HALT
	LD	HL,AMAZCOMM6
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM6+2)
	CP	6
	JR	Z,AMAZINGH
	SUB	4
	LD	(AMAZCOMM6+2),A
	SUB	4
	LD	(AMAZCOMM6+6),A
AMAZINGH:
	POP	BC
	DJNZ	AMAZINGG
	XOR	A
	LD	(AMAZCOMM7+6),A
	;NOSH naar beneden
	LD	B,73
AMAZINGI:
	PUSH	BC
	EI
	HALT
	LD	A,(AMAZCOMM7+6)
	SRL	A
	SRL	A
	INC	A
	AND	3
	LD	C,A
	LD	B,0
	LD	HL,NOSHDOWN
	ADD	HL,BC
	LD	A,(HL)
	LD	(AMAZCOMM7+2),A
	LD	HL,AMAZCOMM7
	CALL	IVDPCOMM
	LD	HL,AMAZCOMM7+6
	INC	(HL)
	POP	BC
	DJNZ	AMAZINGI
	LD	B,20
	CALL	PAUSE
	LD	A,220
	LD	(AMAZCOMM21),A
	LD	HL,AMAZCOMM21
	CALL	IVDPCOMM
	LD	A,219
	LD	(SPRITEATR+96),A
	LD	A,85
	LD	(SPRITEATR+92),A
	LD	A,30
	LD	(NOSHPOOT),A
	LD	HL,CLEARPOOT
	LD	(AMAZINT2+1),HL
	LD	HL,FMEFF7
	LD	DE,PSGEFF26
	CALL	EFFECTOR
	LD	A,121
	LD	(AMAZCOMM9+2),A
	LD	A,123
	LD	(AMAZCOMM9+6),A
	DI
	LD	BC,0B413H
	CALL	WRTVDP
	;G naar beneden
	LD	B,62
AMAZINGJ:
	PUSH	BC
	EI
	HALT
	LD	HL,AMAZCOMM9
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM9+2)
	ADD	A,2
	LD	(AMAZCOMM9+2),A
	ADD	A,2
	LD	(AMAZCOMM9+6),A
	POP	BC
	DJNZ	AMAZINGJ
	;bom afvuren
	LD	HL,PSGEFF23	;PSIEEEEEEEWWW
	CALL	SETPSGEFF
	LD	A,30
	LD	(SPRITEATR+80),A
	LD	(SPRITEATR+84),A
	LD	HL,BOMBOOG
	CALL	SETWAYADR
	LD	HL,(SPRITEATR+80)
AMAZINGK:
	EI
	HALT
	PUSH	HL
	CALL	WAYHEEN
	POP	HL
	LD	A,B
	CP	31
	JR	Z,AMAZINGL
	ADD	HL,BC
	LD	(SPRITEATR+80),HL
	LD	(SPRITEATR+84),HL
	JR	AMAZINGK
AMAZINGL:
	DI
	LD	BC,0D413H
	CALL	WRTVDP
	CALL	READINT
	EI
	LD	HL,PALLETWIT
	LD	DE,PALLET4
	LD	A,2
	CALL	LETFADE
	LD	A,219
	LD	(SPRITEATR+80),A
	LD	(SPRITEATR+84),A
	LD	HL,FMEFF12
	LD	DE,PSGEFF25
	CALL	EFFECTOR
	LD	HL,AMAZCOMM10
	CALL	IVDPCOMM
	LD	IX,SPRITEATR
	CALL	AMAZSETY
	LD	HL,MOVEDEZ
	LD	(AMAZINT2+4),HL
	;de A laten knipperen in rood
	LD	B,11
AMAZINGM:
	PUSH	BC
	LD	B,6
	CALL	PAUSE
	LD	HL,AMAZCOMM11
	CALL	IVDPCOMM
	POP	BC
	DJNZ	AMAZINGM
	DI
	LD	BC,06E13H
	CALL	WRTVDP
	CALL	READINT
	EI
	HALT
	LD	HL,PSGEFF29
	CALL	SETPSGEFF
	LD	BC,00A0AH
AMAZINGN:
	PUSH	BC
	LD	HL,AMAZCOMM12
	CALL	IVDPCOMM
	POP	BC
	PUSH	BC
	LD	B,C
	CALL	PAUSE
	LD	HL,AMAZCOMM13
	CALL	IVDPCOMM
	POP	BC
	PUSH	BC
	LD	B,C
	CALL	PAUSE
	POP	BC
	DEC	C
	DJNZ	AMAZINGN
	;A iets naar rechts
	LD	HL,AMAZCOMM14
	CALL	IVDPCOMM
	EI
	HALT
	LD	HL,FMEFF6
	LD	DE,PSGEFF30
	CALL	EFFECTOR
	LD	B,2
	CALL	PAUSE
	LD	HL,AMAZCOMM17
	CALL	IVDPCOMM
	LD	HL,AMAZCOMM16
	CALL	IVDPCOMM
	LD	HL,AMAZCOMM15
	CALL	IVDPCOMM
	LD	A,117
	LD	(AMAZCOMM18),A
	LD	A,121
	LD	(AMAZCOMM18+4),A
	LD	A,90
	LD	(AMAZCOMM19+6),A
	LD	HL,MAKEAWIT
	LD	(AMAZINT2+1),HL
	LD	B,13
AMAZINGO:
	PUSH	BC
	EI
	HALT
	LD	HL,AMAZCOMM18
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM18)
	ADD	A,4
	LD	(AMAZCOMM18),A
	ADD	A,4
	LD	(AMAZCOMM18+4),A
	POP	BC
	DJNZ	AMAZINGO
	LD	HL,FMEFF10
	LD	DE,PSGEFF25
	CALL	EFFECTOR
	LD	HL,NBOOG
	CALL	SETWAYADR
	EI
	HALT
	LD	HL,AMAZCOMM20
	CALL	IVDPCOMM
	LD	IX,SPRITEATR+32
	CALL	AMAZSETY
	LD	HL,MOVEDEN
	LD	(AMAZINT2+4),HL
	LD	HL,AMAZCOMM23
	CALL	IVDPCOMM
	LD	HL,AMAZCOMM24
	CALL	IVDPCOMM
	;MA naar links
	LD	B,9
	LD	A,104
AMAZINGP:
	LD	(AMAZCOMM25),A
	SUB	2
	LD	(AMAZCOMM25+4),A
	PUSH	BC
	EI
	HALT
	LD	HL,AMAZCOMM25
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM25)
	SUB	2
	POP	BC
	DJNZ	AMAZINGP
	LD	B,85
	CALL	PAUSE
	LD	HL,AMAZCOMM22
	CALL	IVDPCOMM
	EI
	HALT
	DI
	CALL	CLEARSPR
	LD	A,151
	LD	(AMAZCOMM26),A
	LD	A,90
	LD	(AMAZCOMM26+2),A
	DEC	A
	LD	(AMAZCOMM26+6),A
	LD	A,153
	LD	(AMAZCOMM26+4),A
	DI
	LD	BC,05F13H
	CALL	WRTVDP
	CALL	READINT
	LD	HL,AMAZVERPL
	LD	(AMAZINT2+1),HL
	EI

	LD	B,20
	CALL	PAUSE
	LD	HL,AMAZTUSPAL
	LD	DE,AMAZTUSPAL
	LD	A,1
	CALL	LETFADE
	LD	B,6
	CALL	PAUSE

	LD	A,62
	LD	(AMAZCOMM1+4),A
	LD	IX,BUBANMADAT
	LD	B,4
SETNEWANMA:
	PUSH	BC
	LD	A,(IX+0)
	LD	(AMAZCOMM1),A
	LD	HL,AMAZCOMM1
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM1+4)
	ADD	A,34
	LD	(AMAZCOMM1+4),A
	POP	BC
	INC	IX
	DJNZ	SETNEWANMA

	LD	HL,AMAZTUSPAL
	LD	DE,BUBBLEPAL
	LD	A,3
	CALL	LETFADE

	DI
	LD	BC,0D413H
	CALL	WRTVDP
	LD	HL,0F400H
	OR	A
	CALL	SETWRVRAM
	LD	BC,512
AMAZINGQ:
	LD	A,6
	OUT	(098H),A
	DEC	BC
	LD	A,B
	OR	C
	JR	NZ,AMAZINGQ

SETPRESEN:
	LD	A,122
	LD	(AMAZCOMM27),A
	LD	A,68
	LD	(AMAZCOMM27+2),A
	LD	A,82
	LD	(AMAZCOMM27+4),A
	LD	IX,PRESDATA
	LD	B,8
SETPRESENB:
	PUSH	BC
	LD	BC,00A08H
	CALL	IWRTVDP
	LD	A,(IX+1)
	LD	(SETPRESENR),A
	LD	C,(IX+0)
	LD	A,C
	CP	70
	LD	A,088H
	JR	C,SETPRESENE
	LD	A,098H
SETPRESENE:
	LD	(SETPRESENF),A
	LD	(SETPRESENG),A
	INC	IX
	INC	IX
	CALL	VULATRIBUT
	LD	HL,FMEFF7
	LD	BC,PSGEFF41
	LD	DE,PSGEFF38
	CALL	EFFECTOR2
	LD	HL,AMAZSPRON
	LD	(AMAZINT2+1),HL
	EI
	HALT
	HALT
	LD	B,8
SETPRESENC:
	PUSH	BC
	LD	A,(SPRITEATR+126)
	SUB	4
	LD	C,A
	CP	72
	LD	A,0
	JR	C,SETPRESENJ
	LD	A,087H
SETPRESENJ:
	LD	(SETPRESENK),A
	LD	(SETPRESENL),A
	LD	IY,SPRITEATR+124
	LD	HL,SETPRESENR
	LD	DE,0FFFCH
SETPRESENI:
	LD	A,(IY+0)
	CP	219
	JR	Z,SETPRESENH
	LD	(IY+2),C
	LD	A,(IY+3)
	LD	B,A
	SRA	A
	SRA	A
	SRA	A
	SRA	A
SETPRESENK:
	ADD	A,A
	ADD	A,(IY+0)
	ADD	A,9
	LD	(IY+0),A
	LD	A,B
	AND	15
	CP	8
	JR	C,SETPRESENL
	OR	240
SETPRESENL:
	ADD	A,A
	LD	B,0
	RLC	(HL)
SETPRESENF:
	ADC	A,B
	RLC	(HL)
SETPRESENG:
	ADC	A,B
	RRC	(HL)
	RRC	(HL)
	ADD	A,(IY+1)
	LD	(IY+1),A
	ADD	IY,DE
	JR	SETPRESENI
SETPRESENH:
	EI
	HALT
	HALT
	POP	BC
	RLC	(HL)
	RLC	(HL)
	DJNZ	SETPRESENC
	CALL	CLEARSPR
	LD	HL,AMAZCOMM27
	CALL	IVDPCOMM
	LD	A,(AMAZCOMM27)
	ADD	A,8
	LD	(AMAZCOMM27),A
	CP	154
	JR	NZ,EERSTEHEL
	LD	A,122
	LD	(AMAZCOMM27),A
	LD	A,(AMAZCOMM27+2)
	ADD	A,8
	LD	(AMAZCOMM27+2),A
EERSTEHEL:
	LD	A,(AMAZCOMM27+4)
	ADD	A,12
	LD	(AMAZCOMM27+4),A
	POP	BC
	DEC	B
	JP	NZ,SETPRESENB

	LD	B,130
	CALL	PAUSE

	LD	HL,BUBBLEPAL
	LD	DE,PALLETLEEG
	LD	A,2
	CALL	LETFADE
	LD	B,20
	CALL	PAUSE

	JP	SETTITEL

BUBANMADAT:
	DB	192,224,160,192

AMAZTUSPAL: 
	DB	000H,000H,033H,003H,055H,005H,077H,007H
	DB   027H,003H,077H,007H,026H,002H,057H,005H
	DB   022H,002H,040H,000H,070H,000H,073H,003H
	DB   027H,006H,077H,007H,050H,000H,004H,000H

BUBBLEPAL:
	DB	000H,000H,030H,000H,050H,000H,070H,000H
	DB   027H,003H,050H,000H,026H,002H,057H,005H
	DB   022H,002H,040H,000H,070H,000H,073H,003H
	DB   027H,006H,074H,004H,050H,000H,004H,000H

SETPRESENR:
	DB	0

	;Invoer:IX=pointer naar matrix van letter
	;	 C=start x-coordinaat van grote letter
	;

VULATRIBUT:
	LD	A,7
	CALL	IGETKEYS
	AND	00001000B
	OR	017H
	LD	(VULATR2+1),A

	LD	IY,SPRITEATR+124
	LD	DE,04000H
	LD	B,8
VULATR:
	PUSH	BC
	LD	HL,00D70H
	LD	A,(IX+0)
	LD	B,8
VULATR2:
	RR	A
	PUSH	AF
	JR	NC,VULATR3
	LD	A,D
	OR	H
	LD	(IY+3),A
	LD	(IY+2),96
	LD	A,C
	ADD	A,L
	LD	(IY+1),A
	LD	A,28
	ADD	A,E
	LD	(IY+0),A
	PUSH	BC
	LD	BC,0FFFCH
	ADD	IY,BC
	POP	BC
VULATR3:
	LD	A,L
	SUB	16
	LD	L,A
	LD	A,H
	INC	A
	AND	15
	LD	H,A
	POP	AF
	DJNZ	VULATR2
	INC	IX
	LD	A,E
	ADD	A,16
	LD	E,A
	LD	A,D
	SUB	16
	AND	240
	LD	D,A
	POP	BC
	DJNZ	VULATR
	LD	(IY+0),219
	RET

INITREDSCR:
	CALL	CLEARSPR
	;zet zwart blokje
	LD	HL,0F0CEH
	LD	(SPRITEATR),HL
	LD	A,96
	LD	(SPRITEATR+2),A
	CALL	SPRTOVRAM
	LD	HL,0F400H
	OR	A
	CALL	SETWRVRAM
	LD	B,16
	XOR	A
REDSCRSPR:
	OUT	(098H),A
	NOP
	DJNZ	REDSCRSPR
	RET

AMAZSPRON:
	LD	BC,00808H
	CALL	WRTVDP
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	RET

AMAZVERPL:
	LD	HL,AMAZCOMM26
	CALL	VDPCOMM
	LD	A,(AMAZCOMM26)
	CP	193
	JR	Z,AMAZVERPL2
	ADD	A,2
	LD	(AMAZCOMM26),A
	ADD	A,2
	LD	(AMAZCOMM26+4),A
	LD	A,(AMAZCOMM26+2)
	DEC	A
	LD	(AMAZCOMM26+2),A
	DEC	A
	LD	(AMAZCOMM26+6),A
	RET
AMAZVERPL2:
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	RET

AMAZSETY:
	LD	A,89
	LD	(IX+0),A
	LD	(IX+4),A
	LD	(IX+8),A
	LD	(IX+12),A
	LD	A,105
	LD	(IX+16),A
	LD	(IX+20),A
	LD	(IX+24),A
	LD	(IX+28),A
	RET

MAKEAWIT:
	LD	HL,AMAZCOMM19
	CALL	VDPCOMM
	LD	A,(AMAZCOMM19+6)
	INC	A
	LD	(AMAZCOMM19+6),A
	CP	122
	RET	NZ
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	RET

MOVEDEN:
	CALL	WAYHEEN
	LD	A,B
	CP	31
	JR	Z,MOVEDEN2
	OR	C
	JR	Z,MOVEDEN3
	LD	IX,SPRITEATR+32
	LD	H,8
	JP	AMAZSPROPT
MOVEDEN2:
	LD	HL,NIETS
	LD	(AMAZINT2+4),HL
	LD	HL,FMEFF5
	LD	DE,PSGEFF4
	JP	EFFECTOR
MOVEDEN3:
	LD	HL,FMEFF11
	LD	DE,PSGEFF15
	JP	EFFECTOR

	;de Z in een boog bewegen
MOVEDEZ:
	LD	BC,001FEH
	LD	IX,SPRITEATR
	LD	H,4
	CALL	AMAZSPROPT
	LD	BC,0FF03H
	LD	H,4
	LD	IX,SPRITEATR+16
	CALL	AMAZSPROPT
	LD	A,(SPRITEATR)
	CP	219
	RET	NZ
	LD	HL,NIETS
	LD	(AMAZINT2+4),HL
	RET

AMAZSPROPT:
	LD	A,(IX+0)
	CP	219
	JR	Z,AMAZSPROP2
	ADD	A,C
	CP	240
	JR	NC,AMAZSPROP2
	CP	214
	JR	C,AMAZSPROP2
	LD	A,219
AMAZSPROP2:
	LD	(IX+0),A
	LD	A,(IX+1)
	ADD	A,B
	LD	(IX+1),A
	LD	DE,4
	ADD	IX,DE
	DEC	H
	JR	NZ,AMAZSPROPT
	RET

CLEARPOOT:
	LD	HL,NOSHPOOT
	DEC	(HL)
	LD	A,(HL)
	OR	A
	JR	Z,CLEARPOOTB
	CP	20
	RET	NZ
	LD	A,219
	LD	(SPRITEATR+92),A
	LD	A,238
	LD	(AMAZCOMM21),A
	LD	HL,AMAZCOMM21
	JP	VDPCOMM

CLEARPOOTB:
	LD	HL,NOSHRIGHT
	LD	(AMAZINT2+1),HL
	LD	A,209
	LD	(AMAZCOMM8+4),A
	RET
NOSHPOOT:
	DB	0
NOSHRIGHT:
	LD	A,(AMAZCOMM8+4)
	SRL	A
	SRL	A
	AND	3
	LD	C,A
	LD	B,0
	LD	HL,NOSHRIGDAT
	ADD	HL,BC
	LD	A,(HL)
	LD	(AMAZCOMM8),A
	LD	HL,AMAZCOMM8
	CALL	VDPCOMM
	LD	HL,AMAZCOMM8+4
	INC	(HL)
	LD	A,(HL)
	OR	A
	RET	NZ
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	RET

AMAZKOGEL:
	LD	A,(SPRITEATR+88)
	INC	A
	LD	(SPRITEATR+88),A
	CP	177
	RET	C
	LD	A,219
	LD	(SPRITEATR+88),A
	LD	(SPRITEATR+64),A
	LD	(SPRITEATR+68),A
	LD	HL,FMEFF3
	LD	DE,PSGEFF30
	CALL	EFFECTOR
	XOR	A
	LD	(AMAZPLTEL),A
	LD	A,180
	LD	(SPRITEATR+72),A
	LD	(SPRITEATR+76),A
	LD	HL,PLASPLOF
	LD	(AMAZINT2+1),HL
	RET

PLASPLOF:
	LD	HL,AMAZPLTEL
	INC	(HL)
	LD	A,(HL)
	AND	7
	CP	7
	RET	NZ
	LD	A,(SPRITEATR+74)
	ADD	A,4
	CP	128
	JR	Z,PLASPLOFB
	LD	(SPRITEATR+74),A
	ADD	A,28
	LD	(SPRITEATR+78),A
	RET
PLASPLOFB:
	LD	A,219
	LD	(SPRITEATR+72),A
	LD	(SPRITEATR+76),A
	LD	HL,NIETS
	LD	(AMAZINT2+1),HL
	RET
AMAZPLTEL:
	DB	0

AMAZINT2:
	CALL	NIETS
	CALL	NIETS
	CALL	JOYSTICK
	OR	A
	JR	Z,AMAZINT1
AMAZINT2XX: 
	DI
	LD	SP,(AMAZSTACK)
	JP	ONETITEL
AMAZINT1:
	CALL	REGELFADE
	CALL	SPRTOVRAM
	CALL	EFFPSGBEG
	CALL	EFFFMPBEG
	JP	REPLAYROUT

AMAZINT3:
	NOP
AMAZINT3B:
	CALL	REGELFADE
	CALL	SPRTOVRAM
	CALL	EFFPSGBEG
	CALL	EFFFMPBEG
	JP	REPLAYROUT
	
	; The data below is unused, because the same data (FM sound effects) is already in intro.asm.
	; The 'UNUSED_' prefix was added (30-12-2020) to ensure a binary that will be the same 
	; as the original binary (FRANTIC1.BIN) without errors being generated.
	; Somehow, the original assembler (GEN80.COM) did not complain about the duplicate labels.
UNUSED_FMEFF1:
	DB	254,030H,030H,010H,120,020H,0,253,254,020H,28,255,030H,035H
	DB	0,1,03FH,030H,253,253,252,254,030H,0C0H,010H,98,020H,0,253
	DB	254,020H,28,255,0C0H,0C8H,0,1,030H,0C0H,253,253,252
	DB	254,010H,0,251
UNUSED_FMEFF2:
	DB	254,030H,020H,020H,0,020H,30,255,100,0,1,5,010H,100,253,252,251
UNUSED_FMEFF3:
	DB	254,030H,050H,010H,0,020H,0,253,254,020H,26,253,254,030H
	DB	040H,255,70,5,1,5,010H,70,253,252,255,20,0,1,1,010H,20,253
	DB	252,253,254,030H,15,251
UNUSED_FMEFF4:
	DB	254,030H,0C0H,010H,0,020H,0,253,254,020H,26,255,1,120,0,1
	DB	03AH,1,253,248,010H,214,252,253,254,010H,0,020H,18,251
UNUSED_FMEFF5:	
	DB	254,030H,060H,010H,0,020H,0,253,254,020H,22,255,0,120,0,20
	DB	010H,0,253,248,010H,0,253,252,254,010H,0,251
UNUSED_FMEFF6:
	DB	254,030H,050H,010H,0,253,254,020H,0,020H,30,253,253,254,030H
	DB	040H,253,255,120,0,1,15,010H,120,253,248,010H,192,253,252
	DB	254,010H,0,030H,15,251
UNUSED_FMEFF7:
	DB	254,030H,058H,010H,0,253,254,020H,0,020H,26,253,253,254,030H
	DB	040H,253,255,150,20,1,10,010H,150,253,252,254,010H,0,030H,04FH,251
UNUSED_FMEFF8:
	DB	254,030H,0C0H,010H,60,253,255,1,4,0,1,03BH,1,250,1,4,0,1,03AH
	DB	1,253,248,010H,74,254,020H,0,020H,30,249,253,248,010H
	DB	152,252,253,254,010H,0,251
UNUSED_FMEFF9:
	DB	254,030H,030H,253,255,25,4,1,3,010H,25,253,253,254,020H,0
	DB	020H,24,253,253,253,252,254,010H,0,251
UNUSED_FMEFF10:
	DB	254,030H,0B0H,253,255,31,4,1,3,010H,31,253
	DB	254,020H,0,020H,24,253,252,254,010H,0,251
UNUSED_FMEFF11:
	DB	254,030H,0F0H,010H,170,020H,0,253,254,020H,20,255,1,10,0,1
	DB	03AH,1,253,253,248,030H,70,253,253,253,248,030H,187,252
	DB	254,010H,0,251
UNUSED_FMEFF12:
	DB	254,030H,060H,020H,0,253,254,020H,24,255,30,10,1,1,010H,30
	DB	253,248,010H,74,253,252,254,010H,0,251
UNUSED_FMEFF13:
	DB	254,030H,010H,020H,0,020H,24,253,255,1,3,0,1,03AH,1
	DB	254,010H,200,253,254,010H,100,253,252,254,010H,0
	DB	255,1,4,0,1,03AH,1,253,252,255,1,10,0,1,03AH,1
	DB	254,010H,199,253,254,010H,98,253,252,254,010H,0,251
